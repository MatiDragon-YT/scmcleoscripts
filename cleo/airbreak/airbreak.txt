{$CLEO .cs}

03A4: name_thread "AIRBREAK"

const
    VK_SHIFT = 0x10
    VK_CTRL = 0x11
    VK_ALT = 0x12
    VK_SPACE = 0x20
    VK_D = 0x44
    VK_Q = 0x51
    VK_S = 0x53
    VK_X = 0x58
    VK_Z = 0x5A
    VK_F10 = 0x79
end

:MAIN_LOOP
0001: wait 0 ms
00D6: if and
0AB0:   key_pressed VK_X
0AB0:   key_pressed VK_ALT
004D: jump_if_false @MAIN_LOOP
018C: play_sound 1083 at 0.0 0.0 0.0
02AB: set_actor $PLAYER_ACTOR immunities BP 1 FP 1 EP 1 CP 1 MP 1
02A9: set_actor $PLAYER_ACTOR immune_to_nonplayer 1
00D6: if
00DF:   actor $PLAYER_ACTOR driving
004D: jump_if_false @NOCAR
03C0: 0@ = actor $PLAYER_ACTOR car
099A: set_car 0@ collision_detection 0
00AA: store_car 0@ position_to 3@ 4@ 5@
0002: @ACTIVATION_RELEASED_DELAY
:NOCAR
00A0: store_actor $PLAYER_ACTOR position_to 3@ 4@ 5@
0002: @ACTIVATION_RELEASED_DELAY

:ACTIVATION_RELEASED_DELAY
0001: wait 0 ms
00D6: if
8AB0:   not key_pressed VK_X
004D: jump_if_false @ACTIVATION_RELEASED_DELAY

:ACTIVE_LOOP
0001: wait 0 ms
00D6: if or
0AB0:   key_pressed VK_X
0118:   actor $PLAYER_ACTOR dead
004D: jump_if_false @DO_AIRBREAK
018C: play_sound 1084 at 0.0 0.0 0.0
02AB: set_actor $PLAYER_ACTOR immunities BP 0 FP 0 EP 0 CP 0 MP 0
02A9: set_actor $PLAYER_ACTOR immune_to_nonplayer 0
00D6: if
00DF:   actor $PLAYER_ACTOR driving
004D: jump_if_false @MAIN_LOOP
099A: set_car 0@ collision_detection 1
0002: jump @MAIN_LOOP

:DO_AIRBREAK
068D: get_camera_position_to 20@ 21@ 22@
068E: get_camera_target_point_to 22@ 23@ 24@
0063: 22@ -= 20@ // (float)
0063: 23@ -= 21@ // (float)
0604: get_Z_angle_for_point 22@ 23@ store_to 7@
0007: 27@ = 0.0
0007: 28@ = 0.0
0007: 29@ = 0.0

00D6: if
00DF:   actor $PLAYER_ACTOR driving
004D: jump_if_false @SET_ACTOR_ANGLE
0175: set_car 0@ Z_angle_to 7@
0002: jump @DO_KEYCHECK
:SET_ACTOR_ANGLE
0173: set_actor $PLAYER_ACTOR Z_angle_to 7@
0002: jump @DO_KEYCHECK

:DO_KEYCHECK
00D6: if
0AB0:   key_pressed VK_Z
004D: jump_if_false @DO_KEYCHECK_BACK
000F: 27@ -= 1.5

:DO_KEYCHECK_BACK
00D6: if
0AB0:   key_pressed VK_S
004D: jump_if_false @DO_KEYCHECK_LEFT
000B: 27@ += 1.5

:DO_KEYCHECK_LEFT
00D6: if
0AB0:   key_pressed VK_Q
004D: jump_if_false @DO_KEYCHECK_RIGHT
000F: 28@ -= 1.5

:DO_KEYCHECK_RIGHT
00D6: if
0AB0:   key_pressed VK_D
004D: jump_if_false @DO_KEYCHECK_DOWN
000B: 28@ += 1.5

:DO_KEYCHECK_DOWN
00D6: if
0AB0:   key_pressed VK_SHIFT
004D: jump_if_false @DO_KEYCHECK_UP
000F: 29@ -= 0.9

:DO_KEYCHECK_UP
00D6: if
0AB0:   key_pressed VK_SPACE
004D: jump_if_false @DO_KEYCHECK_SPEED
000B: 29@ += 0.9

:DO_KEYCHECK_SPEED
00D6: if
0AB0:   key_pressed VK_CTRL
004D: jump_if_false @DOPUTPOS
0013: 27@ *= 7.0
0013: 28@ *= 7.0

:DOPUTPOS
0087: 26@ = 27@ // (float)
02F6: 14@ = sin 7@ // (float)
006B: 26@ *= 14@  // (float)
0087: 25@ = 26@ // (float)
0087: 26@ = 28@ // (float)
02F7: 14@ = cos 7@ // (float)
006B: 26@ *= 14@  // (float)
005B: 25@ += 26@  // (float)
007B: 3@ += frame_delta_time * 25@ // (float)
0087: 26@ = 27@ // (float)
02F7: 14@ = cos 7@ // (float)
0013: 14@ *= -1.0
006B: 26@ *= 14@  // (float)
0087: 25@ = 26@ // (float)
0087: 26@ = 28@ // (float)
02F6: 14@ = sin 7@ // (float)
006B: 26@ *= 14@  // (float)
005B: 25@ += 26@  // (float)
007B: 4@ += frame_delta_time * 25@ // (float)
007B: 5@ += frame_delta_time * 29@ // (float)
00D6: if
00DF:   actor $PLAYER_ACTOR driving
004D: jump_if_false @ONFOOT
00AB: put_car 0@ at 3@ 4@ 5@
0002: @ACTIVE_LOOP
:ONFOOT
00A1: put_actor $PLAYER_ACTOR at 3@ 4@ 5@
0002: @ACTIVE_LOOP
